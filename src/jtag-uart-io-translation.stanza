#use-added-syntax(jitx)
defpackage jitx-fpga/jtag-uart-io-translation :
  import core
  import collections
  import jitx
  import jitx/commands
  import ocdb/utils/generic-components

;The signals that can be level translated.
public pcb-bundle jtag-uart :
  port tck : pin
  port tms : pin
  port tdi : pin
  port tdo : pin
  port srst_n : pin
  port por_n : pin
  port uart-tx : pin
  port uart-rx : pin

public pcb-module module :
  ;Port connections.
  port n1V8
  port n3V3
  port gnd
  port clk
  port sig-1V8:jtag-uart
  port sig-3V3:jtag-uart

  ;Level translator.
  inst trans : database-part(["mpn" => "NVT2010PW,118", "manufacturer" => "NXP Semicon"])

  ;Power the chips.
  net (n1V8, trans.VREFA)
  net (gnd, trans.GND)

  ;Port connections
  net (sig-1V8.tck, trans.A1)
  net (sig-1V8.tms, trans.A2)
  net (sig-1V8.tdi, trans.A3)
  net (sig-1V8.srst_n, trans.A4)
  net (sig-1V8.tdo, trans.A5)
  net (sig-1V8.por_n, trans.A6)
  net (sig-1V8.uart-tx, trans.A7)
  net (sig-1V8.uart-rx, trans.A8)

  net (sig-3V3.tck, trans.B1)
  net (sig-3V3.tms, trans.B2)
  net (sig-3V3.tdi, trans.B3)
  net (sig-3V3.srst_n, trans.B4)
  net (sig-3V3.tdo, trans.B5)
  net (sig-3V3.por_n, trans.B6)
  net (sig-3V3.uart-tx, trans.B7)
  net (sig-3V3.uart-rx, trans.B8)

  ;Unused
  no-connect(trans.A9)
  no-connect(trans.A10)
  no-connect(trans.B9)
  no-connect(trans.B10)

  ;Explicit net names and symbols.
  let :
    net n1V8 (n1V8)
    net n3V3 (n3V3)
    net gnd (gnd)
    net sig-1V8:jtag-uart (sig-1V8)
    net sig-3V3:jtag-uart (sig-3V3)
    symbol(n1V8) = ocdb/utils/symbols/supply-sym
    symbol(n3V3) = ocdb/utils/symbols/supply-sym
    symbol(gnd) = ocdb/utils/symbols/ground-sym

  ;Decoupling caps.
  cap-strap(trans.EN, gnd, 100.0e-9)

  ;Pullups.
  defn pullup (p:JITXObject, v:Double) : res-strap(p, n3V3, v)
  pullup(trans.VREFB, 200.0e3)
  net (trans.EN, trans.VREFB)
  pullup(trans.B1, 4.75e3)
  pullup(trans.B2, 4.75e3)
  pullup(trans.B3, 4.75e3)
  pullup(trans.B4, 4.75e3)
  pullup(trans.B5, 4.75e3)
  pullup(trans.B6, 4.75e3)
  pullup(trans.B7, 4.75e3)
  pullup(trans.B8, 4.75e3)
